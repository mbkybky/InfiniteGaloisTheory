/-
Copyright (c) 2024 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Nailin Guan, Yuyang Zhao, Yongle Hu, Youle Fang
-/
import Mathlib.Algebra.Category.Grp.Basic
import Mathlib.Topology.Category.Profinite.Basic
import Mathlib.Topology.Algebra.ContinuousMonoidHom
import InfiniteGaloisTheory.New.MissingLemmas.Topology
/-!

# Category of Profinite Groups

We say `G` is a profinite group if it is a topological group which is compact and totally
disconnected.

## Main definitions and results

* `ProfiniteGrp` is the type of profinite groups.

* `FiniteGrp` is the type of finite groups.

* `Pi.profiniteGrp` : The product of profinite groups is also a profinite group.

-/

suppress_compilation

universe u

open CategoryTheory Topology

/--The category of finite groups -/
@[pp_with_univ]
structure FiniteGrp where
  /--A group that is finite-/
  toGrp : Grp
  [isFinite : Finite toGrp]

/--The category of finite groups -/
@[pp_with_univ]
structure FiniteAddGrp where
  /--A add group that is finite-/
  toAddGrp : AddGrp
  [isFinite : Finite toAddGrp]

attribute [to_additive] FiniteGrp

namespace FiniteGrp

@[to_additive]
instance : CoeSort FiniteGrp.{u} (Type u) where
  coe G := G.toGrp

@[to_additive]
instance : Category FiniteGrp := InducedCategory.category FiniteGrp.toGrp

@[to_additive]
instance : ConcreteCategory FiniteGrp := InducedCategory.concreteCategory FiniteGrp.toGrp

@[to_additive]
instance (G : FiniteGrp) : Group G := inferInstanceAs <| Group G.toGrp

@[to_additive]
instance (G : FiniteGrp) : Finite G := G.isFinite

@[to_additive]
instance (G H : FiniteGrp) : FunLike (G ‚ü∂ H) G H :=
  inferInstanceAs <| FunLike (G ‚Üí* H) G H

@[to_additive]
instance (G H : FiniteGrp) : MonoidHomClass (G ‚ü∂ H) G H :=
  inferInstanceAs <| MonoidHomClass (G ‚Üí* H) G H

/--Construct a term of `FiniteGrp` from a type endowed with the structure of a finite group.-/
@[to_additive]
def of (G : Type u) [Group G] [Finite G] : FiniteGrp where
  toGrp := Grp.of G
  isFinite := ‚Äπ_‚Ä∫

/--Construct a term of `FiniteAddGrp` from a type endowed with the structure of a
  finite additive group.-/
add_decl_doc FiniteAddGrp.of

/--The morphisms between FiniteGrp-/
@[to_additive]
def ofHom {X Y : Type u} [Group X] [Finite X] [Group Y] [Finite Y] (f : X ‚Üí* Y) : of X ‚ü∂ of Y :=
  Grp.ofHom f

/--The morphisms between FiniteAddGrp-/
add_decl_doc FiniteAddGrp.ofHom

@[to_additive]
lemma ofHom_apply {X Y : Type u} [Group X] [Finite X] [Group Y] [Finite Y] (f : X ‚Üí* Y) (x : X) :
    ofHom f x = f x :=
  rfl

end FiniteGrp

/--
The category of profinite groups. A term of this type consists of a profinite
set with a topological group structure.
-/
@[pp_with_univ]
structure ProfiniteGrp where
  /-- The underlying profinite topological space. -/
  toProfinite : Profinite
  /-- The group structure. -/
  [group : Group toProfinite]
  /-- The above data together form a topological group. -/
  [topologicalGroup : TopologicalGroup toProfinite]

namespace ProfiniteGrp

instance : CoeSort ProfiniteGrp (Type u) where
  coe G := G.toProfinite

attribute [instance] group topologicalGroup

instance : Category ProfiniteGrp where
  Hom A B := ContinuousMonoidHom A B
  id A := ContinuousMonoidHom.id A
  comp f g := ContinuousMonoidHom.comp g f

instance (G H : ProfiniteGrp) : FunLike (G ‚ü∂ H) G H :=
  inferInstanceAs <| FunLike (ContinuousMonoidHom G H) G H

instance (G H : ProfiniteGrp) : MonoidHomClass (G ‚ü∂ H) G H :=
  inferInstanceAs <| MonoidHomClass (ContinuousMonoidHom G H) G H

instance (G H : ProfiniteGrp) : ContinuousMapClass (G ‚ü∂ H) G H :=
  inferInstanceAs <| ContinuousMapClass (ContinuousMonoidHom G H) G H

instance : ConcreteCategory ProfiniteGrp where
  forget :=
  { obj := fun G => G
    map := fun f => f }
  forget_faithful :=
    { map_injective := by
        intro G H f g h
        exact DFunLike.ext _ _ <| fun x => congr_fun h x }

/--A topological group that is compact and totally disconnected is profinite-/
def of (G : Type u) [Group G] [TopologicalSpace G] [TopologicalGroup G]
    [CompactSpace G] [TotallyDisconnectedSpace G] : ProfiniteGrp where
  toProfinite := .of G
  group := ‚Äπ_‚Ä∫
  topologicalGroup := ‚Äπ_‚Ä∫

@[simp]
theorem coe_of (X : ProfiniteGrp) : (of X : Type _) = X :=
  rfl

@[simp]
theorem coe_id (X : ProfiniteGrp) : (ùüô ((forget ProfiniteGrp).obj X)) = id :=
  rfl

@[simp]
theorem coe_comp {X Y Z : ProfiniteGrp} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    ((forget ProfiniteGrp).map f ‚â´ (forget ProfiniteGrp).map g) = g ‚àò f :=
  rfl

/--A topological group when considered as a topological space is profinite is profinite-/
abbrev ofProfinite (G : Profinite) [Group G] [TopologicalGroup G] :
    ProfiniteGrp := of G

/--The product of profinite group is profinite-/
def pi {Œ± : Type u} (Œ≤ : Œ± ‚Üí ProfiniteGrp) : ProfiniteGrp :=
  let pitype := Profinite.pi fun (a : Œ±) => (Œ≤ a).toProfinite
  letI (a : Œ±): Group (Œ≤ a).toProfinite := (Œ≤ a).group
  letI : Group pitype := Pi.group
  letI : TopologicalGroup pitype := Pi.topologicalGroup
  ofProfinite pitype

/--A FiniteGrp when given the discrete topology can be condsidered as a profinite group-/
def ofFiniteGrp (G : FiniteGrp) : ProfiniteGrp :=
  letI : TopologicalSpace G := ‚ä•
  letI : DiscreteTopology G := ‚ü®rfl‚ü©
  letI : TopologicalGroup G := {}
  of G

instance : HasForget‚ÇÇ FiniteGrp ProfiniteGrp where
  forget‚ÇÇ :=
  { obj := ofFiniteGrp
    map := fun f => ‚ü®f, by continuity‚ü© }

end ProfiniteGrp
